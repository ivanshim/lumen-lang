# Slice string from start (inclusive) to end (exclusive)
fn substring(s, start, end)
    index = start
    out = ""
    while index < end
        out = out . char_at(s, index)
        index = index + 1
    out

# Slice string from start to the end
fn substring_end(s, from)
    substring(s, from, len(s))

# Slice string from the beginning to end (exclusive)
fn substring_start(s, to)
    substring(s, 0, to)

# True if s begins with prefix
fn starts_with(s, prefix)
    len(prefix) <= len(s) and substring(s, 0, len(prefix)) == prefix

# True if s ends with suffix
fn ends_with(s, suffix)
    len(suffix) <= len(s) and substring(s, len(s) - len(suffix), len(s)) == suffix

# Repeat string repetitions times
fn repeat_string(s, repetitions)
    out = ""
    i = 0
    while i < repetitions
        out = out . s
        i = i + 1
    out

# Join array of strings with a separator
fn join_strings(arr, separator)
    out = ""
    n = len(arr)
    i = 0
    while i < n
        if i > 0
            out = out . separator
        out = out . arr[i]
        i = i + 1
    out

# Index of first occurrence of needle in s (or -1)
fn index_of(s, needle)
    n = len(needle)
    i = 0
    while i + n <= len(s)
        if substring(s, i, i + n) == needle
            return i
        i = i + 1
    -1

# True if needle appears in s
fn has_substring(s, needle)
    index_of(s, needle) >= 0
