# Slice string from start (inclusive) to end (exclusive)
fn substring(s, start, end)
    let i = start
    let out = ""
    while i < end
        out = out . char_at(s, i)
        i = i + 1
    out

# Slice string from start to the end
fn slice(s, start)
    substring(s, start, len(s))

# True if s begins with prefix
fn starts_with(s, prefix)
    len(prefix) <= len(s) and substring(s, 0, len(prefix)) == prefix

# True if s ends with suffix
fn ends_with(s, suffix)
    len(suffix) <= len(s) and substring(s, len(s) - len(suffix), len(s)) == suffix

# Repeat string count times
fn repeat(s, count)
    let out = ""
    let i = 0
    while i < count
        out = out . s
        i = i + 1
    out

# Join array of strings with a separator
fn join(arr, sep)
    let out = ""
    let n = len(arr)
    let i = 0
    while i < n
        if i > 0
            out = out . sep
        out = out . arr[i]
        i = i + 1
    out

# Index of first occurrence of needle in s (or -1)
fn index_of(s, needle)
    let n = len(needle)
    let i = 0
    while i + n <= len(s)
        if substring(s, i, i + n) == needle
            return i
        i = i + 1
    -1

# True if needle appears in s
fn contains(s, needle)
    index_of(s, needle) >= 0
